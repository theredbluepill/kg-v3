docs := "docs/"
py_src := "python/"
py_scripts := "scripts/"
py_tests := "tests/"
all_py_code := f"{{py_src}} {{py_scripts}} {{py_tests}}"
all_rs_code := "src/"

[group: 'docs']
docs-lint:
	uvx pymarkdownlnt scan *.md
	uvx pymarkdownlnt scan --recurse {{all_py_code}} {{all_rs_code}} {{docs}}
[group: 'docs']
docs-fresh:
	uv run python scripts/check_doc_freshness.py

[group: 'python']
py-format:
    uvx ruff check {{all_py_code}} --select I --fix
    uvx ruff format {{all_py_code}}
[group: 'python']
py-lint:
    uv run python scripts/check_python_311_syntax.py
    uvx ruff check {{all_py_code}}
[group: 'python']
py-static:
    uv run mypy {{py_src}} {{py_scripts}}
[group: 'python']
py-test:
    uv run pytest {{py_tests}} -m "not slow"
[group: 'python']
py-test-full:
    uv run pytest {{py_tests}}
[group: 'python']
py-prepare: py-format py-lint py-static py-test docs-fresh

[group: 'rust']
rs-format:
    uv run python scripts/check_engine_trim.py
    uv run --offline python scripts/check_opponent_import.py
    cargo fmt
    cargo fmt --manifest-path engine_rs/Cargo.toml --check
    cargo fmt --manifest-path opponents_rs/Cargo.toml --check
[group: 'rust']
rs-lint:
    cargo clippy --all-targets -- -D warnings
    # The Kaggle submission build drops the fixed-opponent controllers.
    cargo clippy --no-default-features -- -D warnings
    # Six pinned upstream style findings; see ops/rebuild-2026-09-29/1.1/results.md.
    cargo clippy --manifest-path engine_rs/Cargo.toml --all-targets --locked -- -D warnings -A clippy::too_many_arguments -A clippy::collapsible_if -A clippy::needless_range_loop
    cargo clippy --offline --manifest-path opponents_rs/Cargo.toml --all-targets --locked -- -D warnings
[group: 'rust']
rs-test:
	cargo test
	cargo test --manifest-path engine_rs/Cargo.toml --locked
	cargo test --offline --manifest-path opponents_rs/Cargo.toml --locked
[group: 'rust']
rs-prepare: rs-format rs-lint rs-test docs-fresh

[group: 'build']
build:
	uv run maturin develop
[group: 'build']
build-release:
	uv run maturin develop --release
[group: 'build']
kaggle-image: prepare
	docker buildx build \
	  --platform linux/amd64 \
	  -f Dockerfile.kaggle \
	  --output type=docker,name=orbit-wars:kaggle,compression=zstd,compression-level=1 \
	  .
[group: 'build']
kaggle-submission model submission="submission" quantization="fp32" *extra_args: prepare kaggle-image
	#!/usr/bin/env bash
	set -euo pipefail
	submission="{{submission}}"
	quantization="{{quantization}}"
	lora_quantization=""
	extra_args=({{extra_args}})
	if [[ -z "$submission" || "$submission" == *"/"* || "$submission" == "." || "$submission" == ".." ]]; then
	  echo "Submission name must be a non-empty file name, not a path: $submission" >&2
	  exit 2
	fi
	model_abs="$(cd "$(dirname "{{model}}")" && pwd)/$(basename "{{model}}")"
	fallback_model_abs=""
	while [[ ${#extra_args[@]} -gt 0 ]]; do
	  case "${extra_args[0]}" in
	    --fallback-checkpoint)
	      if [[ ${#extra_args[@]} -lt 2 ]]; then
	        echo "--fallback-checkpoint requires a path argument" >&2
	        exit 2
	      fi
	      fallback_model_abs="$(cd "$(dirname "${extra_args[1]}")" && pwd)/$(basename "${extra_args[1]}")"
	      extra_args=("${extra_args[@]:2}")
	      ;;
	    --lora-quantization)
	      if [[ ${#extra_args[@]} -lt 2 ]]; then
	        echo "--lora-quantization requires a format argument" >&2
	        exit 2
	      fi
	      lora_quantization="${extra_args[1]}"
	      extra_args=("${extra_args[@]:2}")
	      ;;
	    *)
	      echo "Unexpected kaggle-submission argument: ${extra_args[0]}" >&2
	      exit 2
	      ;;
	  esac
	done
	if [[ "$submission" == *.tar.gz ]]; then
	  output="artifacts/${submission}"
	else
	  output="artifacts/${submission}.tar.gz"
	fi
	output_abs="$(mkdir -p "$(dirname "$output")" && cd "$(dirname "$output")" && pwd)/$(basename "$output")"
	submission_args=()
	docker_args=(-v "$(dirname "$model_abs"):/model:ro")
	if [[ -n "$fallback_model_abs" ]]; then
	  submission_args+=(--fallback-checkpoint "/fallback-model/$(basename "$fallback_model_abs")")
	  docker_args+=(-v "$(dirname "$fallback_model_abs"):/fallback-model:ro")
	fi
	if [[ "$quantization" != "fp32" ]]; then
	  submission_args+=(--quantization "$quantization")
	fi
	if [[ -n "$lora_quantization" ]]; then
	  submission_args+=(--lora-quantization "$lora_quantization")
	fi
	docker run --rm \
	  "${docker_args[@]}" \
	  -v "$(dirname "$output_abs"):/artifacts" \
	  orbit-wars:kaggle "${submission_args[@]}" "/model/$(basename "$model_abs")" "/artifacts/$(basename "$output_abs")"
	artifact_limit_bytes=$((100 * 1024 * 1024))
	artifact_bytes="$(wc -c < "$output_abs" | tr -d '[:space:]')"
	if (( artifact_bytes > artifact_limit_bytes )); then
	  warning="WARNING: Kaggle submission artifact exceeds 100MiB: $output_abs is ${artifact_bytes} bytes"
	  if [[ -t 2 ]]; then
	    printf '\033[31m%s\033[0m\n' "$warning" >&2
	  else
	    printf '%s\n' "$warning" >&2
	  fi
	fi

_prepare_base: build rs-format py-format rs-lint py-lint docs-lint py-static rs-test py-test-full
[group: 'ci']
prepare: _prepare_base docs-fresh
[group: 'ci']
prepare-rl: prepare build-release
[group: 'ci']
prepare-container: _prepare_base build-release

[group: 'misc']
audit-selected-target-angles:
    cargo test --lib rl::action_spec::tests::audit_selected_target_angle_quality -- --ignored --nocapture

[group: 'misc']
clean:
    cargo clean
    rm -rf .mypy_cache .pytest_cache .ruff_cache .venv/
    rm -f python/owl/rs*.so
    rm -f tests/fixtures/**/*.{json,jsonl}
